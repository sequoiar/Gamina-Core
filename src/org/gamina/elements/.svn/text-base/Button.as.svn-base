package com.gamina.elements {
   import com.gamina.api.IWidget;
   import com.gamina.api.IWidgetElement;
   import com.gamina.api.logic.BaseInputCMD;
   import org.osflash.signals.natives.NativeSignal;
   import flash.display.GradientType;
   import flash.display.InterpolationMethod;
   import flash.display.LineScaleMode;
   import flash.display.SpreadMethod;
   import flash.display.Sprite;
   import flash.events.MouseEvent;
   import flash.geom.Matrix;
   import flash.geom.Point;

	public class Button extends Sprite implements IWidgetElement {
		public var recWidth : Number = 130;
		public  var recHeight : Number = 28;
		
		public var COLOR : uint =   0xFFFFFF;
		public var COLOR2 : uint = 0x000000;

		public var ROUND:uint=7;
		public var COLOR_BORDER:uint= 0XFFFC17;
		
		protected var _colRow:Point;

		
		/**
		 * Listen to this for mouse events in presenter
		 */
		public var clickedSignal:NativeSignal;
		
		/**
		 * Only if you want the button. If using TextButton use initTextButton
		 */
		[Deprecated]
		public function initButtonElement(parent : IWidget) : Button {
			if(parent )
				parent.addElement(this);
			select(false);
			
		   clickedSignal = new NativeSignal(this,MouseEvent.MOUSE_DOWN);
		
			return this;
		}
		
		
		public function handleNavCmd(navCmd:BaseInputCMD):void {
			clickedSignal.dispatch(new MouseEvent(MouseEvent.MOUSE_DOWN));
			
		}

		
		public function drawDeselected() : void {
			graphics.clear();
			var box : Matrix = new Matrix();
			box.createGradientBox(recWidth * .90, recHeight * .50, Math.PI / 2);

			graphics.lineStyle(1, COLOR_BORDER, 0.4, true, LineScaleMode.NONE);
			graphics.beginGradientFill(GradientType.LINEAR, [COLOR, COLOR2], [1, 1], [1, 255], box, SpreadMethod.PAD, InterpolationMethod.LINEAR_RGB, -0.39);

			graphics.drawRoundRect(0, 0, recWidth, recHeight, ROUND, ROUND);

			graphics.endFill();
			box = null;
		}

		public function drawSelected() : void {
			graphics.clear();
			var box : Matrix = new Matrix();
			box.createGradientBox(recWidth * 2, recHeight, Math.PI / 2, -recWidth * .5, -recHeight * .1);

			graphics.lineStyle(2, 0x000000, 1, true, LineScaleMode.NONE);
			graphics.beginGradientFill(GradientType.RADIAL, [COLOR, COLOR2], [1, 1], [1, 255], box, SpreadMethod.PAD, InterpolationMethod.LINEAR_RGB, -0.39);
			graphics.drawRoundRect(0, 0, recWidth, recHeight, ROUND, ROUND);

			graphics.endFill();
			box = null;
		}


		public function select(sel : Boolean) : void {
			if(sel)
				drawSelected();
			else
				drawDeselected();
		}

		public function get position() : Point {
			return _colRow;
		}
		
		public function setPosition(col : uint, row : uint) : void {
			_colRow = new Point(col,row);
		}
		
		public  function get className():String {
			return 'Button';
		}

	}
}


	

		