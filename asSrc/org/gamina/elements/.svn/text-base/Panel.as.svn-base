package com.gamina.elements {


   import de.polygonal.ds.SLinkedList;
   import com.gamina.api.IWidgetElement;
   import com.gamina.api.logic.BaseInputCMD;
   import flash.display.DisplayObject;
   import flash.display.GradientType;
   import flash.display.InterpolationMethod;
   import flash.display.LineScaleMode;
   import flash.display.SpreadMethod;
   import flash.display.Sprite;
   import flash.geom.Matrix;
   import flash.geom.Point;

/**
 * Implements Widget so we can add elements. When using Panel, make sure you set the mask
 */
	public class Panel extends Sprite implements IWidgetElement {
	
		public var BACK_COLOR:uint= 0x003366;
		public var BACK_COLOR2:uint= 0x151B54;
		public var COLOR_BORDER:uint= 0XFFFFF0;
		
		public static var  PADDING:uint=6;
		
		protected var _mask:Sprite;
		
		public var content:Sprite;
		
	

		/***
		 * Elements in the panel for  scrolling
		 */
		public var scrollElements:SLinkedList;
		

		/**
		 * Similar to Minimal Components. You must re-draw mask, possibly on first frame
		 */
		public function Panel() {
		
			scrollElements = new SLinkedList();
			
			_mask = new Sprite();
			_mask.mouseEnabled = false;
			super.addChild(_mask);
			
			content = new Sprite();
			super.addChild(content);
			content.mask = _mask;
	
		}


		public static function placeV(o:DisplayObject, relativeTo:DisplayObject):void {
			o.x=relativeTo.x;
			o.y=relativeTo.y+relativeTo.height+PADDING;
		}
		
		public static function placeH(o:DisplayObject, relativeTo:DisplayObject):void {
			o.y=relativeTo.y;
			o.x=relativeTo.x+relativeTo.width+PADDING;
		}
		
		public function handleNavCmd(navCmd:BaseInputCMD):void {
			
			
		}

		
		public  function get className():String {
			return 'Panel';
		}

		/**
		 * required to set mask when using Panel. If you draw rectangles it is set for you
		 */
		public  function drawMask(w_:uint=1024,h_:uint=600):void {
			_mask.graphics.clear();
			_mask.graphics.beginFill(0x000000);
			_mask.graphics.drawRect(0, 0, w_,h_);
			_mask.graphics.endFill();
		}
		
		public override function addChild(child:DisplayObject):DisplayObject
		{
			content.addChild(child);
			return child;
			
		}
		
		
		public function drawRectBackground(w_:uint=1024,h_:uint=600):void {
			graphics.clear();
			var box : Matrix = new Matrix();
			box.createGradientBox(w_*.5,h_*.5,0,-w_/8,-h_/8);
			
			graphics.beginGradientFill(GradientType.RADIAL,[BACK_COLOR,BACK_COLOR2],[1,1],[1,255],box,SpreadMethod.REFLECT,InterpolationMethod.RGB);
		    graphics.drawRect(0,0,w_,h_);
			graphics.endFill();
			cacheAsBitmap=true;
			drawMask(w_,h_);
		}

		
		public function drawRoundBackground(w_:uint=1024,h_:uint=600):void {
			graphics.clear();
			var box : Matrix = new Matrix();
			box.createGradientBox(w_*.5,h_*.5,0,-w_/8,-h_/8);
			
			graphics.lineStyle(2, COLOR_BORDER, 0.8, true, LineScaleMode.NONE);
			graphics.beginGradientFill(GradientType.RADIAL,[BACK_COLOR,BACK_COLOR2],[.2,.6],[1,255],box,SpreadMethod.REFLECT,InterpolationMethod.RGB);
			graphics.drawRoundRect(0,0,w_,h_,8,8);
			graphics.endFill();
			cacheAsBitmap=true;
			drawMask(w_,h_);
		}
		
		public function get position() : Point {
			return null;
		}

		public function setPosition(col_ : uint, row_ : uint) : void {
		}

		public function select(sel : Boolean) : void {
			throw new Error('Implement in concrete class');
		}
	


  }
}
